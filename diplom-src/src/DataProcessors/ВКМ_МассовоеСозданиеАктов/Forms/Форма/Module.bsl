
&НаКлиенте
Процедура МассовоеСозданиеАктов(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Выберите дату расчетного месяца");
		Возврат;
	КонецЕсли;
	ДлительнаяОперация = СоздатьАктыНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СозданиеАктовЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении); 
КонецПроцедуры

&НаСервере
Функция СоздатьАктыНаСервере()
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, 
	"Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьАктыНаСервере", Объект.Период);

КонецФункции

&НаКлиенте
Процедура СозданиеАктовЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	МассивДляТЧ = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ДобавитьСтрокувВТЧОбработки(МассивДляТЧ);
	ОбщегоНазначенияКлиент.СообщитьПользователю("Обработка завершена");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокувВТЧОбработки(МассивДляТЧ)
	
	ТабЧасть = Объект.ДоговорыИРеализации;
	ТабЧасть.Очистить();
	
	Если МассивДляТЧ <> Неопределено Тогда
		Для Каждого Элемент Из МассивДляТЧ Цикл 
			
			Обработки.ВКМ_МассовоеСозданиеАктов.ДобавитьСтрокуВТЧ(Элемент.Договор, Элемент.Реализация, ТабЧасть); 
			
		КонецЦикла; 
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Период) Тогда 
		Элементы.ФормаМассовоеСозданиеАктов.Заголовок = "Массовое создание актов за " + Формат(Объект.Период,"ДФ='ММММ yyyy'");
	КонецЕсли;
КонецПроцедуры
