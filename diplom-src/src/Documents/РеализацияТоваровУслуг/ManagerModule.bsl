
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

//Начало ВКМ_Доработки
Процедура ПриОпределенииНастроеПечати(НастройкиОбъекта)
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры
// Описание:
// Параметры:
// КомандыПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугах";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

//
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанныхУслугах");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ВКМ_СобратьАктОбОказанныхУслугах(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах";
	КонецЕсли;

КонецПроцедуры
//Конец ВКМ_Доработки
#КонецОбласти

//Начало ВКМ_Доработки
#Область СлужебныеПроцедурыИФункции

Функция ВКМ_СобратьАктОбОказанныхУслугах(МассивОбъектов, ОбъектыПечати)

	ТабДок = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Контрагент,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Договор,
	|	РеализацияТоваровУслуг.Услуги.(
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма) КАК Услуги
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьШапка = Макет.ПолучитьОбласть("ШапкаДокумента");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьСуммаИтого = Макет.ПолучитьОбласть("СуммаИтого");
	ОбластьСуммаПрописью = Макет.ПолучитьОбласть("СуммаПрописью");  
	ОбластьПодвал = Макет.ПолучитьОбласть("ПодвалДокумента");
	
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда 
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьШапка.Параметры.Номер = Формат(Число(Выборка.Номер), "ЧГ=;");
		ОбластьШапка.Параметры.Дата = Формат(Выборка.Дата, "ДФ='dd.MM.yyyy ''г.''';");
		ОбластьШапка.Параметры.Организация = Выборка.Организация;
		ОбластьШапка.Параметры.Контрагент = Выборка.Контрагент;
		ОбластьШапка.Параметры.Договор = Выборка.Договор;
		
		ТабДок.Вывести(ОбластьШапка);
		ТабДок.Вывести(ОбластьПустаяСтрока);
		ТабДок.Вывести(ОбластьШапкаТаблицы);
		
		ВыборкаУслуги = Выборка.Услуги.Выбрать();
		
		НомерСтроки = 1;
		СуммаИтого = 0;
		
		Пока ВыборкаУслуги.Следующий() Цикл
			
			ОбластьСтрокаТаблицы.Параметры.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			ОбластьСтрокаТаблицы.Параметры.Услуга = ВыборкаУслуги.Номенклатура;
			ОбластьСтрокаТаблицы.Параметры.Количество = ВыборкаУслуги.Количество;
			ОбластьСтрокаТаблицы.Параметры.Цена = ВыборкаУслуги.Цена;
			ОбластьСтрокаТаблицы.Параметры.Сумма = ВыборкаУслуги.Сумма;
			СуммаИтого = СуммаИтого + ВыборкаУслуги.Сумма;
			
			ТабДок.Вывести(ОбластьСтрокаТаблицы);
			
		КонецЦикла;
		
		ОбластьСуммаИтого.Параметры.СуммаИтого = СуммаИтого;
		ТабДок.Вывести(ОбластьСуммаИтого);
		ТабДок.Вывести(ОбластьПустаяСтрока);
		
		ОбластьСуммаПрописью.Параметры.СуммаПрописью = ЧислоПрописью(СуммаИтого, "Л=ru_RU;ДП=Истина", "рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2");
		ТабДок.Вывести(ОбластьСуммаПрописью);
		ТабДок.Вывести(ОбластьПустаяСтрока);
		ТабДок.Вывести(ОбластьПодвал);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало,
			ОбъектыПечати, Выборка.Ссылка);

	КонецЦикла;
	
	Возврат ТабДок;


КонецФункции

#КонецОбласти
//Конец ВКМ_Доработки
#КонецЕсли